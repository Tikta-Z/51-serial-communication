LED 	 BIT	P2.4
ORG 0000H
		LJMP MAIN
ORG 000BH
		LJMP TIM0
ORG 0023H
		LJMP RECEIVE

ORG 0030H
MAIN:	MOV SCON,#50H	;设置串行口工作方式1,允许接收
		MOV PCON,#00H	;SMOD为0，波特率不加倍
		MOV TMOD,#21H	;定时器1工作方式2,T0工作方式1
		MOV TH1,#0FDH	;溢出率初值
		MOV TL1,#0FDH	
		MOV IE,#92H		;开启中断使能
		CLR PT0			;定时器0低优先级
		SETB PS			;串口中断高优先级
		SETB TR1		;打开定时器
		SETB F0
HERE:	JNB F0,MUSIC
		JMP HERE

MUSIC:	SETB F0
		CPL LED
		LCALL START0
		JMP HERE

RECEIVE:CLR RI
		MOV A,SBUF
		MOV R0,A
		CLR F0	
RETEN:	RETI
		

START0:	
		MOV 30H,#00				;音乐播放子程序
NEXT:	MOV A,30H
		MOV DPH,#00H
		MOV DPL,R0
		MOVC A,@A+DPTR
		MOV R2,A
		JZ END0
		ANL A,#0FH
		MOV R5,A
		MOV A,R2
		SWAP A
		ANL A,#0FH
		JNZ SING
		CLR TR0
		JMP D1
SING:	DEC A
		MOV 22H,A
		RL A
		MOV DPTR,#TABLE1
		MOVC A,@A+DPTR
		MOV TH0,A
		MOV 21H,A
		MOV A,22H
		RL A
		INC A
		MOVC A,@A+DPTR
		MOV TL0,A
		MOV 20H,A
		SETB TR0
D1:		LCALL DELAY		;输出一个音符
		INC 30H
		JMP NEXT
END0:	CLR TR0			;停止播放
		RET
		
TIM0:	PUSH ACC
		PUSH PSW
		MOV TL0,20H		;重新装入初值
		MOV TH0,21H
		CPL P2.7       	;输出音频数据
		POP PSW
		POP ACC
		RETI
		
DELAY:	MOV R7,#02		;延时0.1s×R5
D2:		MOV R4,#100
D3:		MOV R3,#230
		DJNZ R3,$
		DJNZ R4,D3
		DJNZ R7,D2
		DJNZ R5,DELAY
		RET

DELAY1:	MOV R7,#50		;延时3S
D21:	MOV R4,#100
D31:	MOV R3,#250
		DJNZ R3,$
		DJNZ R4,D31
		DJNZ R7,D21
		RET

TABLE1:
		DW 63625,63833,64019,64104,64260,64440,64524			;音律表
		DW 64580,64685,64778,64820,64898,64968,65030
		DW 65058
		;	1	  2		 3    4	     5	   6     7   低音
		;   8	  9	     A	  B 	 C	   D     E  中音
		;   F										 高
		
TABLE:	 DB	0A4H,84H,94H,64H,0A2H,92H,83H,92H,64H 	;高位为音律，低位为节拍时间
		 DB	0A4H,84H,94H,94H,0C2H,0A2H,72H,84H		;00E7
		 DB 00H	

QIJI:   DB	62H,72H,84H,84H,84H,0A2H,0A2H,0A2H
		DB  96H,52H,52H,0A4H,0A4H,0A4H,92H,92H,92H
		DB  86H,84H,72H,62H,62H,86H,84H,72H,52H,52H
		DB	86H,82H,82H,94H,64H,72H,82H,82H,72H,76H
		DB  00H



END