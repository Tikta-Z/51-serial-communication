ORG 0000H
JMP START
ORG 000BH
JMP TIM0
TABLE1:
		DW 63625,63833,64019,64104,64260,64440,64524			;音律表
		DW 64580,64685,64778,64820,64898,64968,65030
		DW 65058
		;	1	  2		 3    4	     5	   6     7   低音
		;   8	  9	     A	  B 	 C	   D     E  中音
		;   F										 高音
TABLE:	DB	64H,84H,0C4H,84H,44H,52H,62H,0C4H,84H 	;高位为音律，低位为节拍时间
		DB	14H,54H,0C4H,84H,14H,0C4H,14H,0C4H
		DB	64H,84H,0C4H,64H,44H,52H,62H,0C4H,84H
		DB  14H,54H,0C4H,84H,14H,0C4H,72H,82H,54H	
		DB  00H									
START:
		MOV TMOD,#01H  		;T0工作方式1
		MOV IE,#92H			
START0:	MOV 30H,#00
NEXT:	MOV A,30H
		MOV DPTR,#TABLE
		MOVC A,@A+DPTR
		MOV R2,A
		JZ END0
		ANL A,#0FH
		MOV R5,A
		MOV A,R2
		SWAP A
		ANL A,#0FH
		JNZ SING
		CLR TR0
		JMP D1
SING:	DEC A
		MOV 22H,A
		RL A
		MOV DPTR,#TABLE1
		MOVC A,@A+DPTR
		MOV TH0,A
		MOV 21H,A
		MOV A,22H
		RL A
		INC A
		MOVC A,@A+DPTR
		MOV TL0,A
		MOV 20H,A
		SETB TR0
D1:		LCALL DELAY		;输出一个音符
		INC 30H
		JMP NEXT
END0:	CLR TR0			;停止播放
		JMP START0
TIM0:	PUSH ACC
		PUSH PSW
		MOV TL0,20H		;重新装入初值
		MOV TH0,21H
		CPL P2.7       	;输出音频数据
		POP PSW
		POP ACC
		RETI
  DELAY:MOV R7,#02		;延时0.13s×R5
    D2:	MOV R4,#100
    D3:	MOV R3,#248
		DJNZ R3,$
		DJNZ R4,D3
		DJNZ R7,D2
		DJNZ R5,DELAY
		RET

END